    # variable addresses
    define IP 0    # current instruction pointer
    define SP 1    # stack top pointer
    define CP 2    # call stack top pointer
    define LP 3    # label list pointer
    define JP 4    # jump list pointer
    define TEMP 5  # temporary pointer

    # allocation sizes
    # 199990:    instruction list size (id/argument pairs)
    # 1000:      stack size
    # 1000:      call stack size
    # 1000:      label list size (label/line pairs)
    # 1000:      jump list size
    # unbounded: heap size

    # boundaries for allocation blocks
    # label list and jump list blocks are reused by stack and call stack
    define IMIN 10      # instruction list start
    define IMAX 200000  # instruction list end
    define SMIN 200000  # stack start
    define SMAX 201000  # stack end
    define CMIN 201000  # call stack start
    define CMAX 202000  # call stack end
    define LMIN 200000  # label list start
    define LMAX 201000  # label list end
    define JMIN 201000  # jump list start
    define JMAX 202000  # jump list end
    define HMIN 202000  # heap start

    # assigning initial values
    push IP; push IMIN; store
    push SP; push SMIN; store
    push CP; push CMIN; store
    push LP; push LMIN; store
    push JP; push JMIN; store

    # instruction ids in order of time to find in .select_instr
    define INSTR_1    0  # selects in 2 instructions
    define INSTR_2    6  # selects in 8 instructions
    define INSTR_3   -6
    define INSTR_4    9  # selects in 14 instructions
    define INSTR_5    3
    define INSTR_6   -3
    define INSTR_7   -9
    define INSTR_8   10  # selects in 20 instructions
    define INSTR_9    8
    define INSTR_10   4
    define INSTR_11   2
    define INSTR_12  -2
    define INSTR_13  -4
    define INSTR_14  -8
    define INSTR_15 -10
    define INSTR_16  11  # selects in 21 instructions
    define INSTR_17   7
    define INSTR_18   5
    define INSTR_19   1
    define INSTR_20  -1
    define INSTR_21  -5
    define INSTR_22  -7
    define INSTR_23 -11

    # instructions in order of most common
    define DO_INSTR_1  .do_push;     define INSTR_PUSH     INSTR_1
    define DO_INSTR_2  .do_dup;      define INSTR_DUP      INSTR_2
    define DO_INSTR_3  .do_store;    define INSTR_STORE    INSTR_3
    define DO_INSTR_4  .do_sub;      define INSTR_SUB      INSTR_4
    define DO_INSTR_5  .do_jz;       define INSTR_JZ       INSTR_5
    define DO_INSTR_6  .do_retrieve; define INSTR_RETRIEVE INSTR_6
    define DO_INSTR_7  .do_jmp;      define INSTR_JMP      INSTR_7
    define DO_INSTR_8  .do_call;     define INSTR_CALL     INSTR_8
    define DO_INSTR_9  .do_drop;     define INSTR_DROP     INSTR_9
    define DO_INSTR_10 .do_printc;   define INSTR_PRINTC   INSTR_10
    define DO_INSTR_11 .do_ret;      define INSTR_RET      INSTR_11
    define DO_INSTR_12 .do_add;      define INSTR_ADD      INSTR_12
    define DO_INSTR_13 .do_swap;     define INSTR_SWAP     INSTR_13
    define DO_INSTR_14 .do_printi;   define INSTR_PRINTI   INSTR_14
    define DO_INSTR_15 .do_jn;       define INSTR_JN       INSTR_15
    define DO_INSTR_16 .do_end;      define INSTR_END      INSTR_16
    define DO_INSTR_17 .do_mul;      define INSTR_MUL      INSTR_17
    define DO_INSTR_18 .do_readc;    define INSTR_READC    INSTR_18
    define DO_INSTR_19 .do_readi;    define INSTR_READI    INSTR_19
    define DO_INSTR_20 .do_copy;     define INSTR_COPY     INSTR_20
    define DO_INSTR_21 .do_slide;    define INSTR_SLIDE    INSTR_21
    define DO_INSTR_22 .do_div;      define INSTR_DIV      INSTR_22
    define DO_INSTR_23 .do_mod;      define INSTR_MOD      INSTR_23


    # dropped by read_char
    push 0
parse:
    call read_char
    dup; push ' ';  sub; jz .parse_stack
    dup; push '\t'; sub; jz .parse_imp_tab
    dup; push '\n'; sub; jz .parse_flow
    dup; jz setup_labels
    jmp parse
.parse_imp_tab:
    call read_char
    dup; push ' ';  sub; jz .parse_arith
    dup; push '\t'; sub; jz .parse_heap
    dup; push '\n'; sub; jz .parse_io
    dup; jz .error_incomplete_instr_tab
    jmp .parse_imp_tab

.parse_stack:
    call read_char
    dup; push ' ';  sub; jz .parsed_push
    dup; push '\t'; sub; jz .parse_stack_tab
    dup; push '\n'; sub; jz .parse_stack_lf
    dup; jz .error_incomplete_instr_space
    jmp .parse_stack
.parse_stack_tab:
    call read_char
    dup; push ' ';  sub; jz .parsed_copy
    dup; push '\t'; sub; jz .error_invalid_instr_space_tab_tab
    dup; push '\n'; sub; jz .parsed_slide
    dup; jz .error_incomplete_instr_space_tab
    jmp .parse_stack_tab
.parse_stack_lf:
    call read_char
    dup; push ' ';  sub; jz .parsed_dup
    dup; push '\t'; sub; jz .parsed_swap
    dup; push '\n'; sub; jz .parsed_drop
    dup; jz .error_incomplete_instr_space_lf
    jmp .parse_stack_lf

.parse_arith:
    call read_char
    dup; push ' ';  sub; jz .parse_arith_space
    dup; push '\t'; sub; jz .parse_arith_tab
    dup; push '\n'; sub; jz .error_invalid_instr_tab_space_lf
    dup; jz .error_incomplete_instr_tab_space
    jmp .parse_arith
.parse_arith_space:
    call read_char
    dup; push ' ';  sub; jz .parsed_add
    dup; push '\t'; sub; jz .parsed_sub
    dup; push '\n'; sub; jz .parsed_mul
    dup; jz .error_incomplete_instr_tab_space_space
    jmp .parse_arith_space
.parse_arith_tab:
    call read_char
    dup; push ' ';  sub; jz .parsed_div
    dup; push '\t'; sub; jz .parsed_mod
    dup; push '\n'; sub; jz .error_invalid_instr_tab_space_tab_lf
    dup; jz .error_incomplete_instr_tab_space_tab
    jmp .parse_arith_tab

.parse_heap:
    call read_char
    dup; push ' ';  sub; jz .parsed_store
    dup; push '\t'; sub; jz .parsed_retrieve
    dup; push '\n'; sub; jz .error_invalid_instr_tab_tab_lf
    dup; jz .error_incomplete_instr_tab_tab
    jmp .parse_heap

.parse_flow:
    call read_char
    dup; push ' ';  sub; jz .parse_flow_space
    dup; push '\t'; sub; jz .parse_flow_tab
    dup; push '\n'; sub; jz .parse_flow_lf
    dup; jz .error_incomplete_instr_lf
    jmp .parse_arith_tab
.parse_flow_space:
    call read_char
    dup; push ' ';  sub; jz .parsed_label
    dup; push '\t'; sub; jz .parsed_call
    dup; push '\n'; sub; jz .parsed_jmp
    dup; jz .error_incomplete_instr_lf_space
    jmp .parse_flow_space
.parse_flow_tab:
    call read_char
    dup; push ' ';  sub; jz .parsed_jz
    dup; push '\t'; sub; jz .parsed_jn
    dup; push '\n'; sub; jz .parsed_ret
    dup; jz .error_incomplete_instr_lf_tab
    jmp .parse_flow_tab
.parse_flow_lf:
    call read_char
    dup; push ' ';  sub; jz .error_invalid_instr_lf_lf_space
    dup; push '\t'; sub; jz .error_invalid_instr_lf_lf_tab
    dup; push '\n'; sub; jz .parsed_end
    dup; jz .error_incomplete_instr_lf_lf
    jmp .parse_flow_lf

.parse_io:
    call read_char
    dup; push ' ';  sub; jz .parse_io_space
    dup; push '\t'; sub; jz .parse_io_tab
    dup; push '\n'; sub; jz .error_invalid_instr_tab_lf_lf
    dup; jz .error_incomplete_instr_tab_lf
    jmp .parse_io
.parse_io_space:
    call read_char
    dup; push ' ';  sub; jz .parsed_printc
    dup; push '\t'; sub; jz .parsed_printi
    dup; push '\n'; sub; jz .error_invalid_instr_tab_lf_space_lf
    dup; jz .error_incomplete_instr_tab_lf_space
    jmp .parse_flow_space
.parse_io_tab:
    call read_char
    dup; push ' ';  sub; jz .parsed_readc
    dup; push '\t'; sub; jz .parsed_readi
    dup; push '\n'; sub; jz .error_invalid_instr_tab_lf_tab_lf
    dup; jz .error_incomplete_instr_tab_lf_tab
    jmp .parse_flow_tab

# returns parsed signed integer on top of stack
.parse_signed:
    call read_char
    dup; push ' ';  sub; jz .parse_unsigned
    dup; push '\t'; sub; jz .parse_signed_negative
    dup; push '\n'; sub; jz .parse_signed_zero
    dup; jz .error_unterminated_number
    jmp .parse_signed
.parse_signed_negative:
    call .parse_unsigned
    push -1
    mul
    ret
.parse_signed_zero:
    push 0
    ret

# returns parsed unsigned integer on top of stack
.parse_unsigned:
    push 0
.parse_unsigned_loop:
    swap
    call read_char
    dup; push ' ';  sub; jz .parse_unsigned_zero
    dup; push '\t'; sub; jz .parse_unsigned_one
    dup; push '\n'; sub; jz .parse_unsigned_end
    dup; jz .error_unterminated_number
    jmp .parse_unsigned_loop
.parse_unsigned_zero:
    swap
    push 2
    mul
    jmp .parse_unsigned_loop
.parse_unsigned_one:
    swap
    push 2
    mul
    push 1
    add
    jmp .parse_unsigned_loop
.parse_unsigned_end:
    swap
    ret

read_char:
    drop
    push TEMP
    dup
    readc
    retrieve
    ret


# pushes instruction ID and stores instruction in instruction list
.parsed_push:     push INSTR_PUSH;     jmp .parsed_instr_number_arg
.parsed_dup:      push INSTR_DUP;      jmp .parsed_instr
.parsed_copy:     push INSTR_COPY;     jmp .parsed_instr_number_arg
.parsed_swap:     push INSTR_SWAP;     jmp .parsed_instr
.parsed_drop:     push INSTR_DROP;     jmp .parsed_instr
.parsed_slide:    push INSTR_SLIDE;    jmp .parsed_instr_number_arg
.parsed_add:      push INSTR_ADD;      jmp .parsed_instr
.parsed_sub:      push INSTR_SUB;      jmp .parsed_instr
.parsed_mul:      push INSTR_MUL;      jmp .parsed_instr
.parsed_div:      push INSTR_DIV;      jmp .parsed_instr
.parsed_mod:      push INSTR_MOD;      jmp .parsed_instr
.parsed_store:    push INSTR_STORE;    jmp .parsed_instr
.parsed_retrieve: push INSTR_RETRIEVE; jmp .parsed_instr
.parsed_call:     push INSTR_CALL;     jmp .parsed_instr_label_arg
.parsed_jmp:      push INSTR_JMP;      jmp .parsed_instr_label_arg
.parsed_jz:       push INSTR_JZ;       jmp .parsed_instr_label_arg
.parsed_jn:       push INSTR_JN;       jmp .parsed_instr_label_arg
.parsed_ret:      push INSTR_RET;      jmp .parsed_instr
.parsed_end:      push INSTR_END;      jmp .parsed_instr
.parsed_printc:   push INSTR_PRINTC;   jmp .parsed_instr
.parsed_printi:   push INSTR_PRINTI;   jmp .parsed_instr
.parsed_readc:    push INSTR_READC;    jmp .parsed_instr
.parsed_readi:    push INSTR_READI;    jmp .parsed_instr

# adds label to label stack
.parsed_label:
    call .parse_unsigned

    # push LP and address at LP
    push LP
    dup
    retrieve

    # store IP at address at LP+1
    dup
    push 1
    add
    push IP
    retrieve
    store

    # increment LP
    swap
    copy 1
    push 2
    add
    store

    # store label at address at LP
    swap
    store

    jmp parse

.parsed_instr_label_arg:
    swap
    call .parse_unsigned

    # push JP and address at JP
    push JP
    dup
    retrieve

    # store IP+1 at address at LP
    dup
    push IP
    retrieve
    push 1
    add
    store

    # increment LP
    push 1
    add
    store

    jmp .store_instr_arg

.parsed_instr_number_arg:
    swap
    call .parse_signed
    # fallthrough

.store_instr_arg:
    # store argument at IP+1
    push IP
    retrieve
    push 1
    add
    swap
    store

    swap
    # fallthrough

.parsed_instr:
    # push IP and address at IP
    push IP
    dup
    retrieve

    # increment IP
    swap
    copy 1
    push 2
    add
    store

    # store instruction id at IP
    swap
    store

    jmp parse


# replace labels with instruction indices
setup_labels:
    # init jump loop
    push JP
    retrieve
    push JMIN
.jump_loop:
    # check jump loop bounds
    dup
    copy 2
    sub; jz interpret

    # init label loop
    push LP
    retrieve
    push LMIN
.label_loop:
    # check label loop bounds
    dup
    copy 2
    sub; jz .error_label_not_found

    # push current label and label of current jump
    dup
    retrieve
    copy 3
    retrieve
    retrieve
    # check if labels match
    sub; jz .replace_label

.label_loop_continue:
    # increment label loop
    push 1
    add
    jmp .label_loop

.jump_loop_continue:
    # drop label loop bounds
    drop
    drop
    # increment jump loop
    push 1
    add
    jmp .jump_loop

.replace_label:
    # push address of jump and instruction index of label
    copy 2
    retrieve
    copy 1
    push 1
    add
    retrieve
    # replace jump label with index
    store
    jmp .jump_loop_continue


interpret:
    # drop label setup loop bounds
    drop
    drop

    # append end instruction
    push IP
    dup
    retrieve
    push INSTR_END
    store

    # reset IP to IMIN
    push IMIN
    store
    jmp .select_instr

.select_next_instr:
    # increment IP
    push IP
    dup
    retrieve
    push 2
    add
    store
    # fallthrough

# find instruction with binary search
.select_instr:
    # drop temp value
    drop

    # push current instruction ID
    push IP
    retrieve
    retrieve

    dup; jz DO_INSTR_1
    dup; jn .select_instr_L
    # fallthrough
# .select_instr_R:
    push 6; sub
    dup; jz DO_INSTR_2
    dup; jn .select_instr_RL
    # fallthrough
# .select_instr_RR:
    push 3; sub
    dup; jz DO_INSTR_4
    dup; jn .select_instr_RRL
    # fallthrough
# .select_instr_RRR:
    push 1; sub
    dup; jz DO_INSTR_8
    jmp DO_INSTR_16
.select_instr_RRL:
    push 1; add
    dup; jz DO_INSTR_9
    jmp DO_INSTR_17
.select_instr_RL:
    push 3; add
    dup; jz DO_INSTR_5
    dup; jn .select_instr_RLL
    # fallthrough
# .select_instr_RLR:
    push 1; sub
    dup; jz DO_INSTR_10
    jmp DO_INSTR_18
.select_instr_RLL:
    push 1; add
    dup; jz DO_INSTR_11
    jmp DO_INSTR_19
.select_instr_L:
    push 6; add
    dup; jz DO_INSTR_3
    dup; jn .select_instr_LL
    # fallthrough
# .select_instr_LR:
    push 3; sub
    dup; jz DO_INSTR_6
    dup; jn .select_instr_LRL
    # fallthrough
# .select_instr_LRR:
    push 1; sub
    dup; jz DO_INSTR_12
    jmp DO_INSTR_20
.select_instr_LRL:
    push 1; add
    dup; jz DO_INSTR_13
    jmp DO_INSTR_21
.select_instr_LL:
    push 3; add
    dup; jz DO_INSTR_7
    dup; jn .select_instr_LLL
    # fallthrough
# .select_instr_LLR:
    push 1; sub
    dup; jz DO_INSTR_14
    jmp DO_INSTR_22
.select_instr_LLL:
    push 1; add
    dup; jz DO_INSTR_15
    jmp DO_INSTR_23


.do_push:
    # push 0; push 'h'; push 's'; push 'u'; push 'p'; call .debug_instr_arg

    # push SP and address for top+1 of stack
    push SP
    dup
    retrieve

    # push argument to stack
    dup
    push IP
    retrieve
    push 1
    add
    retrieve
    store

    # increment SP
    push 1
    add
    store

    jmp .select_next_instr

.do_dup:
    # push 0; push 'p'; push 'u'; push 'd'; call .debug_instr

    push 1; call check_stack_underflow

    # push SP and address for top+1 of stack
    push SP
    dup
    retrieve

    # duplicate top of stack
    dup
    dup
    push 1
    sub
    retrieve
    store

    # increment SP
    push 1
    add
    store

    jmp .select_next_instr

.do_copy:
    # push 0; push 'y'; push 'p'; push 'o'; push 'c'; call .debug_instr_arg
    jmp .error_unimplemented_instr

.do_swap:
    # push 0; push 'p'; push 'a'; push 'w'; push 's'; call .debug_instr
    jmp .error_unimplemented_instr

.do_drop:
    # push 0; push 'p'; push 'o'; push 'r'; push 'd'; call .debug_instr

    push 1; call check_stack_underflow

    # decrement SP
    push SP
    dup
    retrieve
    push 1
    sub
    store

    jmp .select_next_instr

.do_slide:
    # push 0; push 'e'; push 'd'; push 'i'; push 'l'; push 's'; call .debug_instr
    jmp .error_unimplemented_instr

.do_add:
    # push 0; push 'd'; push 'd'; push 'a'; call .debug_instr
    call .arith_retrieve
    add
    jmp .arith_store

.do_sub:
    # push 0; push 'b'; push 'u'; push 's'; call .debug_instr
    call .arith_retrieve
    sub
    jmp .arith_store

.do_mul:
    # push 0; push 'l'; push 'u'; push 'm'; call .debug_instr
    call .arith_retrieve
    mul
    jmp .arith_store

.do_div:
    # push 0; push 'v'; push 'i'; push 'd'; call .debug_instr
    call .arith_retrieve
    dup; jz .error_division_by_zero
    div
    jmp .arith_store

.do_mod:
    # push 0; push 'd'; push 'o'; push 'm'; call .debug_instr
    call .arith_retrieve
    mod
    jmp .arith_store

.do_store:
    # push 0; push 'e'; push 'r'; push 'o'; push 't'; push 's'; call .debug_instr

    push 2; call check_stack_underflow

    # push SP and address for top-2 of stack
    push SP
    dup
    retrieve
    push 2
    sub

    # push transformed address at top-2 and value at top-1 of stack
    dup
    retrieve
    call .to_heap_addr
    copy 1
    push 1
    add
    retrieve

    store

    # decrement SP by 2
    store

    jmp .select_next_instr

.do_retrieve:
    # push 0; push 'e'; push 'v'; push 'e'; push 'i'; push 'r'; push 't'; push 'e'; push 'r'; call .debug_instr

    push 1; call check_stack_underflow

    # push SP and address for top-1 of stack
    push SP
    retrieve
    push 1
    sub

    # retrieve heap value and push to stack
    dup
    retrieve
    call .to_heap_addr
    retrieve
    store

    jmp .select_next_instr

.do_call:
    # push 0; push 'l'; push 'l'; push 'a'; push 'c'; call .debug_instr_arg

    # push CP and *CP
    push CP
    dup
    retrieve

    # push *IP+2
    push IP
    retrieve
    push 2
    add

    # store *IP+2 at *CP
    store

    # increment CP
    push 1
    add
    store

    # fallthrough

.do_jmp:
    # push 0; push 'p'; push 'm'; push 'j'; call .debug_instr_arg

    # push IP and address of jmp label
    push IP
    dup
    retrieve
    push 1
    add
    retrieve

    # jump to label
    store

    jmp .select_instr

.do_jz:
    # push 0; push 'z'; push 'j'; call .debug_instr_arg

    call .pop_stack
    jz .do_jmp
    jmp .select_next_instr

.do_jn:
    # push 0; push 'n'; push 'j'; call .debug_instr_arg

    call .pop_stack
    jn .do_jmp
    jmp .select_next_instr

.do_ret:
    # push 0; push 't'; push 'e'; push 'r'; call .debug_instr
    jmp .error_unimplemented_instr

.do_end:
    # push 0; push 'd'; push 'n'; push 'e'; call .debug_instr

    # drop temp value and end
    drop
    end

.do_printc:
    # push 0; push 'c'; push 't'; push 'n'; push 'i'; push 'r'; push 'p'; call .debug_instr

    call .pop_stack
    printc
    jmp .select_next_instr

.do_printi:
    # push 0; push 'i'; push 't'; push 'n'; push 'i'; push 'r'; push 'p'; call .debug_instr

    call .pop_stack
    printi
    jmp .select_next_instr

.do_readc:
    # push 0; push 'c'; push 'd'; push 'a'; push 'e'; push 'r'; call .debug_instr

    call .pop_stack
    call .to_heap_addr
    readc
    jmp .select_next_instr

.do_readi:
    # push 0; push 'i'; push 'd'; push 'a'; push 'e'; push 'r'; call .debug_instr

    call .pop_stack
    call .to_heap_addr
    readi
    jmp .select_next_instr

# returns SP, address for top of stack, address of top-1 of stack, value at top-1 of stack, and value at top of stack
.arith_retrieve:
    # push SP and address for top of stack
    push SP
    dup
    retrieve
    push 1
    sub

    # push address for top-1 of stack
    dup
    push 1
    sub

    # push value at top-1 of stack
    dup
    retrieve

    # push value at top of stack
    copy 2
    retrieve

    ret

.arith_store:
    # store modified value
    store
    # decrement SP
    store
    jmp .select_next_instr

.pop_stack:
    push 1; call check_stack_underflow

    # push SP and address for top of stack
    push SP
    dup
    retrieve
    push 1
    sub

    # decrement SP
    swap
    copy 1
    store

    # push value at top of stack
    retrieve

    ret

# params: addr
.to_heap_addr:
    push 2; mul
    dup; jn .to_heap_addr_neg
.to_heap_addr_done:
    push HMIN; add
    ret
.to_heap_addr_neg:
    push -1; mul
    push 1; add
    jmp .to_heap_addr_done

# params: count
check_stack_underflow:
    # push SP-count and SMIN
    push SP
    retrieve
    swap
    sub
    push SMIN

    # underflow if SP-count < SMIN
    sub; jn .error_stack_underflow
    ret


# expects zero on top of stack
.error_incomplete_instr_space_tab:       call .string_tab;   jmp .error_incomplete_instr_space
.error_incomplete_instr_space_lf:        call .string_lf;    # fallthrough
.error_incomplete_instr_space:           call .string_space; jmp .error_incomplete_instr
.error_incomplete_instr_tab_space_space: call .string_space; jmp .error_incomplete_instr_tab_space
.error_incomplete_instr_tab_space_tab:   call .string_tab;   # fallthrough
.error_incomplete_instr_tab_space:       call .string_space; jmp .error_incomplete_instr_tab
.error_incomplete_instr_tab_tab:         call .string_tab;   jmp .error_incomplete_instr_tab
.error_incomplete_instr_tab_lf_space:    call .string_space; jmp .error_incomplete_instr_tab_lf
.error_incomplete_instr_tab_lf_tab:      call .string_tab;   # fallthrough
.error_incomplete_instr_tab_lf:          call .string_lf;    # fallthrough
.error_incomplete_instr_tab:             call .string_tab;   jmp .error_incomplete_instr
.error_incomplete_instr_lf_space:        call .string_space; jmp .error_incomplete_instr_lf
.error_incomplete_instr_lf_tab:          call .string_tab;   jmp .error_incomplete_instr_lf
.error_incomplete_instr_lf_lf:           call .string_lf;    # fallthrough
.error_incomplete_instr_lf:              call .string_lf;    # fallthrough

.error_incomplete_instr:
    push ' '
    push ':'
    call .string_instruction
    push ' '
    push 'e'
    push 't'
    push 'e'
    push 'l'
    push 'p'
    push 'm'
    push 'o'
    push 'c'
    push 'n'
    push 'i'
    call .println_error
    end

# expects a temp value on stack
.error_invalid_instr_space_tab_tab:    push 0;                  call .string_tab;   call .string_tab;   call .string_space; jmp .error_invalid_instr
.error_invalid_instr_tab_space_lf:     push 0;                  call .string_lf;    call .string_space; call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_tab_space_tab_lf: push 0; call .string_lf; call .string_tab;   call .string_space; call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_tab_tab_lf:       push 0;                  call .string_lf;    call .string_tab;   call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_tab_lf_lf:        push 0;                  call .string_lf;    call .string_lf;    call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_tab_lf_space_lf:  push 0; call .string_lf; call .string_space; call .string_lf;    call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_tab_lf_tab_lf:    push 0; call .string_lf; call .string_tab;   call .string_lf;    call .string_tab;   jmp .error_invalid_instr
.error_invalid_instr_lf_lf_space:      push 0;                  call .string_space; call .string_lf;    call .string_lf;    jmp .error_invalid_instr
.error_invalid_instr_lf_lf_tab:        push 0;                  call .string_tab;   call .string_lf;    call .string_lf;    # fallthrough

.error_invalid_instr:
    push ' '
    push ':'
    call .string_instruction
    push ' '
    push 'd'
    push 'i'
    push 'l'
    push 'a'
    push 'v'
    push 'n'
    push 'i'
    call .println_error
    drop
    end

# expects a temp value, partially parsed number, and zero on stack
.error_unterminated_number:
    push ' '
    push ':'
    push 'r'
    push 'e'
    push 'b'
    push 'm'
    push 'u'
    push 'n'
    push ' '
    push 'd'
    push 'e'
    push 't'
    push 'a'
    push 'n'
    push 'i'
    push 'm'
    push 'r'
    push 'e'
    push 't'
    push 'n'
    push 'u'
    call .print_error
    printi
    push '\n'; printc
    drop
    end

.error_unimplemented_instr:
    push 0
    call .string_instruction
    push ' '
    push 'd'
    push 'e'
    push 't'
    push 'n'
    push 'e'
    push 'm'
    push 'e'
    push 'l'
    push 'p'
    push 'm'
    push 'i'
    push 'n'
    push 'u'
    call .println_error
    drop
    end

# expects temp value, loop bound, address of address in jump list, and two loop bounds on stack
.error_label_not_found:
    push 0
    push ' '
    push ':'
    push 'd'
    push 'n'
    push 'u'
    push 'o'
    push 'f'
    push ' '
    push 't'
    push 'o'
    push 'n'
    push ' '
    push 'l'
    push 'e'
    push 'b'
    push 'a'
    push 'l'
    call .print_error
    drop
    drop
    retrieve; retrieve; printi
    push '\n'; printc
    drop
    drop
    end

# expects temp value on stack
.error_stack_underflow:
    push 0
    push 'w'
    push 'o'
    push 'l'
    push 'f'
    push 'r'
    push 'e'
    push 'd'
    push 'n'
    push 'u'
    push ' '
    push 'k'
    push 'c'
    push 'a'
    push 't'
    push 's'
    call .println_error
    drop
    end

# expects four values, numerator, and zero (denominator) on stack
.error_division_by_zero:
    push ' '
    push ':'
    push 'o'
    push 'r'
    push 'e'
    push 'z'
    push ' '
    push 'y'
    push 'b'
    push ' '
    push 'n'
    push 'o'
    push 'i'
    push 's'
    push 'i'
    push 'v'
    push 'i'
    push 'd'
    call .print_error
    printi
    push 0
    push '\n'
    push '0'
    push '/'
    call print
    drop
    drop
    drop
    drop
    end

.string_space:
    push ']'
    push 'e'
    push 'c'
    push 'a'
    push 'p'
    push 'S'
    push '['
    ret
.string_tab:
    push ']'
    push 'b'
    push 'a'
    push 'T'
    push '['
    ret
.string_lf:
    push ']'
    push 'F'
    push 'L'
    push '['
    ret
.string_error:
    push ' '
    push ':'
    push 'r'
    push 'o'
    push 'r'
    push 'r'
    push 'E'
    ret
.string_instruction:
    push 'n'
    push 'o'
    push 'i'
    push 't'
    push 'c'
    push 'u'
    push 'r'
    push 't'
    push 's'
    push 'n'
    push 'i'
    ret

.println_error:
    call .string_error
    jmp println
.print_error:
    call .string_error
    jmp print

.debug_instr:
    jmp println
.debug_instr_arg:
    call print
    push ' '; printc
    push IP
    retrieve
    push 1
    add
    retrieve
    printi
    push '\n'; printc
    ret

# print prints a string on the stack terminated with 0
# params: char..., 0
print:
    dup; jz .print_done
    printc
    jmp print
.print_done:
    drop
    ret

# println prints a string on the stack terminated with 0 and a LF
# params: char..., 0
println:
    call print
    push '\n'; printc
    ret
